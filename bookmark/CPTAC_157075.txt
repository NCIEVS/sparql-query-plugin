#### Report of CPTAC concepts in a given subset
#### To change subset, change the subset code in the line that starts with "values"


prefix ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
select distinct (str(?ncicode) as ?code) (str(?ncipt) as ?nci_pt)  (str(?cptacpt) as ?cptac_pt) (str(?cptacsy) as ?cptac_sy) (str(?ncidef) as ?nci_def)
where {
    values ?some_subset { ncit:C157075}

    ?s ncit:NHC0 ?ncicode ;
       rdfs:label ?ncipt ;
      ncit:A8 ?some_subset .
    [] a owl:Axiom ;
      owl:annotatedSource ?s ;
      owl:annotatedTarget ?cptacpt ;
      owl:annotatedProperty ncit:P90 ;
      ncit:P384 ?tsp ;
      ncit:P383 ?ttp;
    filter regex (str(?ttp), "PT")
    filter regex (str(?tsp), "CPTAC")
    optional {
        [] a owl:Axiom ;
          owl:annotatedSource ?s ;
          owl:annotatedTarget ?cptacsy ;
          owl:annotatedProperty ncit:P90 ;
          ncit:P384 ?tss ;
          ncit:P383 ?tts;
        filter regex (str(?tts), "SY")
        filter regex (str(?tss), "CPTAC")
    }
    optional {
        [] a owl:Axiom ;
          owl:annotatedSource ?s ;
          owl:annotatedTarget ?ncidef ;
          owl:annotatedProperty ncit:P97 ;
    }
}
